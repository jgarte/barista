#!/bin/env janet
(import neil/tell :prefix "")
(import neil/utils)
(import path)
(import curl)
(import json)

(defn capt [label]
  ~(* (some (if-not ,label 1)) ,label :s+ '(some (if-not "\n" 1))))

(def capg
  ~(* ,(capt "state:")
      ,(capt "energy-rate:")
      (? ,(capt '(* "time to " (<- (+ "empty" "full")) ":")))
      ,(capt "percentage:")))

(def days ["ne" "po" "ůt" "st" "čt" "pá" "so"])

(defn fetch-weather
  []

  (def c (curl/easy/init))
  (def b @"")
  (:setopt c
           :url "api.openweathermap.org/data/2.5/weather?q=Nové Město pod Smrkem&units=metric&appid=a548e43683bfe87042ec22ac0a967095"
           :write-function |(buffer/push-string b $)
           :no-progress? true)

  (def res (:perform c))
  (when (not (zero? res))
    (error (string "Cannot fetch: " (curl/easy/strerror res))))
  (string/format "\ufa8f %.1f°C " (get-in (json/decode b) ["main" "temp"])))

(defn main [_ &opt ntb]
  (default ntb false)
  (var weather (fetch-weather))
  (forever
    (def start (os/clock))
    (def n (os/date (os/time) true))
    (when (zero? (mod (n :minutes) 15)) (set weather (fetch-weather)))
    (def ts (string/format " \uf5ef %s %i/%s/%s %i:%s:%s"
                           (days (n :week-day))
                           (n :year)
                           (utils/pad (inc (n :month)))
                           (utils/pad (inc (n :month-day)))
                           (n :hours)
                           (utils/pad (n :minutes))
                           (utils/pad (n :seconds))))
    (def task
      (string "\uf5d6 "
              (match
                (protect
                  (tell
                    (if-let [r (:task/running neil)]
                      (let [[rid {:name n :project pid :work-intervals iw}] r
                            {:name p :client cid} (:by-id neil pid)
                            {:abbrev c} (:by-id neil cid)
                            {:start s :note t} (last iw)]
                        (string/format "\uf04b %s $%s @%s - #%s %s" (utils/durf (- (os/time) s)) c p rid n))
                      (let [[rid {:name n :project pid :work-intervals iw}] (:task/last-ran neil)
                            {:name p :client cid} (:by-id neil pid)
                            {:abbrev c} (:by-id neil cid)
                            dur (reduce (fn [r x] (+ r (- (x :end) (x :start)))) 0 iw)]
                        (string/format "\uf04c %s $%s @%s #%s - %s" (utils/durf dur) c p rid n)))))
                [true m] m
                [false _] "Office err!")))
    (def netp
      (string
        "\uf700 "
        (string/join (seq [[a p] :pairs {"ocean-one" 443 "ocean-three" 443 "neil" 6660}]
                       (match (protect (with [c (net/connect (string a ".laststar.work") p)]))
                         [true _] "\uf00c"
                         [false _] "\uf00d")) " ")
        " "))
    (if ntb
      (let [lp |(-> "/sys/class/backlight/intel_backlight/" (path/join $0) slurp string/trim scan-number)
            light (->> (lp "max_brightness") (/ (lp "actual_brightness")) (* 100) math/floor)
            batt (->> "/sys/class/power_supply/BAT0/capacity" slurp string/trim)]
        (printf "%s %s %s \uf5dd %s%% \uf578 %s %s" weather task light batt ts netp))
      (printf "%s %s %s %s" weather task ts netp))
    (flush)
    (def dur (- (os/clock) start))
    (unless (>= dur 1) (os/sleep (- 1 dur)))))
