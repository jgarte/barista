#!/bin/env janet
(import neil/tell :prefix "")
(import neil/utils)
(import path)
(import curl)
(import json)

(defn capt [label]
  ~(* (some (if-not ,label 1)) ,label :s+ '(some (if-not "\n" 1))))

(def capg
  ~(* ,(capt "state:")
      ,(capt "energy-rate:")
      (? ,(capt '(* "time to " (<- (+ "empty" "full")) ":")))
      ,(capt "percentage:")))

(def days ["ne" "po" "ůt" "st" "čt" "pá" "so"])

(def appid (string/trim (slurp "/home/pp/.config/barista/wmp")))
(def place (string/trim (slurp "/home/pp/.config/barista/place")))

(defn fetch-weather
  []
  (def c (curl/easy/init))
  (def b @"")
  (:setopt c
           :url (string "api.openweathermap.org/data/2.5/weather?q=" place "&units=metric&appid=" appid)
           :write-function |(buffer/push-string b $)
           :no-progress? true)
  (def res (:perform c))
  (when (not (zero? res))
    (error (string "Cannot fetch: " (curl/easy/strerror res))))
  (def m ((json/decode b) "main"))
  (string/format "\ufa8f %.1f °C | %.1f °C | %i%% | %i hPa" (m "feels_like") (m "temp") (m "humidity") (m "pressure")))

(defn neilcheck []
  (forever
    (def r
      (string "\uf5d6 "
              (match
                (protect
                  (tell
                    (if-let [r (:task/running neil)]
                      (let [[rid {:name n :project pid :work-intervals iw}] r
                            {:name p :client cid} (:by-id neil pid)
                            {:abbrev c} (:by-id neil cid)
                            {:start s :note t} (last iw)]
                        (string/format "\uf04b %s $%s @%s - #%s %s" (utils/durf (- (os/time) s)) c p rid n))
                      (let [[rid {:name n :project pid :work-intervals iw}] (:task/last-ran neil)
                            {:name p :client cid} (:by-id neil pid)
                            {:abbrev c} (:by-id neil cid)
                            dur (reduce (fn [r x] (+ r (- (x :end) (x :start)))) 0 iw)]
                        (string/format "\uf04c %s $%s @%s #%s - %s" (utils/durf dur) c p rid n)))))
                [true m] m
                [false e] "loading")))
    (yield r)))

(defn print-line [ntb]
  (var weather (fetch-weather))
  (def neilcheckf (ev/call neilcheck))
  (forever
    (def start (os/clock))
    (def n (os/date (os/time) true))
    (when (and (zero? (n :seconds)) (zero? (mod (n :minutes) 15))) (set weather (fetch-weather)))
    (def ts (string/format " \uf5ef %s %i/%s/%s %i:%s:%s"
                           (days (n :week-day))
                           (n :year)
                           (utils/pad (inc (n :month)))
                           (utils/pad (inc (n :month-day)))
                           (n :hours)
                           (utils/pad (n :minutes))
                           (utils/pad (n :seconds))))
    (def task
      (if (fiber/can-resume? neilcheckf) (resume neilcheckf) "\uf5d6 loading"))
    (def netp
      (string
        "\uf700 "
        (string/join (seq [[a p] :pairs {"ocean-one" 443 "ocean-three" 443 "neil" 6660}]
                       (match (protect (with [c (net/connect (string a ".laststar.work") p)]))
                         [true _] "\uf00c"
                         [false _] "\uf00d")) " ")
        " "))
    (if ntb
      (let [lp |(-> "/sys/class/backlight/intel_backlight/" (path/join $0) slurp string/trim scan-number)
            light (->> (lp "max_brightness") (/ (lp "actual_brightness")) (* 100) math/floor (string "\uf5dd "))
            batt (->> "/sys/class/power_supply/BAT0/capacity" slurp string/trim (string "\uf578 "))]
        (printf "%s %s %s%% %s%% %s %s" weather task light batt ts netp))
      (printf "%s %s %s %s" weather task ts netp))
    (flush)
    (def dur (- (os/clock) start))
    (unless (>= dur 1) (ev/sleep (- 1 dur)))))

(defn main [_ &opt ntb]
  (default ntb false)
  (def f (ev/call print-line ntb))
  (ev/go f))
